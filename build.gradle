buildscript {
	ext {
		springBootVersion = '1.5.1.RELEASE'
	}
	repositories {
		mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    	classpath "com.moowork.gradle:gradle-node-plugin:1.1.1"
	}
}

plugins {
    id "com.moowork.node" version "1.1.1"
} 

node {
    version = '5.7.0'
    npmVersion = '3.9.2'
    download = true
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'reactjs-spring-data-rest-gradle'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-data-rest')
	compile('org.springframework.boot:spring-boot-starter-thymeleaf')
	runtime('com.h2database:h2')
	compileOnly('org.projectlombok:lombok')
	testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:'1.5.1.RELEASE') {
		exclude(module: 'commons-logging')
	}
}



task webpack(type: NodeTask, dependsOn: 'npmInstall') {
	script = project.file('node_modules/.bin/webpack')
}
task webpackClean(type: Delete) {
    delete './src/main/resources/static/built'
}
clean.dependsOn webpackClean

task npmClean (type: Delete) { delete './node_modules' }

processResources.dependsOn 'webpack'

task cleanAll(dependsOn: [clean, npmClean] )